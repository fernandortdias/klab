namespace ses
	"Fundamental socio-environmental system concepts. The SES is seen as a 
	 network of interactions. This namespace contains mostly roles
	 for independently specified observables, which define a system from the
	 perspective of a SES."
	using im
	in domain im:EnvironmentalSocialScience;

abstract role Demand
	"This role is mandatory for one or more outputs of any process model that has the Uptake role. It
	 allows k.LAB to identify the demand for a beneficiary."
	applies to im:Quantity;

abstract role Supply
	"This role is mandatory for one or more outputs of any process model that has the Provision role. It
	 allows k.LAB to identify the usable output of a Provider."
	applies to im:Quantity;

abstract role ValueExchanged
	"This role is mandatory for one or more outputs of any process model that has the Uptake role. It
	 allows k.LAB to identify the value exchanged at the transactor."
	applies to im:Quantity;

abstract role ValueTransferred
	"This role is mandatory for flows of value between providers, transactors and beneficiaries. It
	 allows models to compute total flow from connections."
	is im:Throughput
	applies to im:Quantity
	has children
		(abstract ValueProvided
			"The value transferred between provider and transactor."
		),
		(abstract ValueUsed
			"The value transferred between transactor and beneficiary."
		); 

abstract role Effector
	"Effectors are processes or events that generate social damage and whose effects are mitigated by
	 regulation services." 
	applies to im:Process, im:Event;

abstract role Provision
	"This role is given to the processes that define ES provision within providers. Adopting the
	 role makes it mandatory to define output qualities."
	applies to im:Process
	implies at least 1 Supply;

abstract role SocialUptake
	"This role is given to the processes that define ES uptake within beneficiaries. Adopting the
	 role makes it mandatory to define demand qualities."
	applies to im:Process
	implies exactly 1 Demand;
	
abstract role ValueExchange
	"The role given to the processes that define value generation within transactors. Adopting the
	 role makes it mandatory to define both output and demand qualities."
	applies to im:Process
	implies exactly 1 ValueExchanged;

abstract role FlowRole 
	"The main roles that characterize each agent in a service flow network. They should be
	 specialized for each service type."
	applies to im:Subject
	has children
				(abstract Provider
					implies Supply, Provision
				),
				(abstract Beneficiary
					implies Demand, SocialUptake
				),
				(abstract Sink
					"Anything that has the Sink role detracts from the Supply, so it implies it."
					implies Supply
				),
				(abstract Transactor
					implies ValueExchange, ValueExchanged
				);

deniable attribute Rival
	"Rivality in SES interactions qualifies the uptake process and defines whether the satisfaction of
	 demand reduces the ability of other beneficiaries to do the same."
	applies to SocialUptake 
	is im:RelationalAttribute;

abstract role Flow
	"Any flow that carries ecosystem benefits."
	applies to im:TransferConnection
	has children 
		(
			abstract EcosystemBenefitFlow 
				"Generic ecosystem benefit connects a provider with a beneficiary. This
			 	may be broken down into provision and use when transactors can be identified."
				applies to im:TransferConnection between Provider and Beneficiary
		),
		(
			abstract ProvisionFlow 
				"Generic ecosystem service provision connects a provider with a transactor."
				applies to im:TransferConnection between Provider and Transactor
		),
		(
			abstract UseFlow
				"Generic ecosystem service use connects a beneficiary with a transactor."
				applies to im:TransferConnection between Beneficiary and Transactor
		)
	implies ValueTransferred
;	

/**
 * Starting point for an ES benefit specification: the process role requires the 
 * flow, and the resolution of the flows triggers the building of the network.
 * 
 * Any ES model is specified by applying the specialized SocioEcologicalExchange role to a process
 * which in turn specializes the EcosystemServiceFlow implication, defining provider and
 * beneficiary. 
 */
abstract role SocioEcologicalExchange
	"This role allows the system to find the processes that define the flow network and
	 the observables that characterize the SES."
	applies to im:TransitiveProcess
	implies at least 1 EcosystemBenefitFlow
;

abstract identity BeneficialMechanism
	"The mechanism through which a socio-ecological exchange becomes beneficial."
	has children
		(Provisioning
			"Value is produced by the provider and directly used by the beneficiary at the transactor."
		),
		(Regulating
			"Value is produced outside of the ES and the provider protects it or its use by mitigating
             the effects of one or more effector processes."
		);

abstract role ValueMaintenance
	"An independent process that creates and maintains the value that regulating services help
	 protect from the consequences of the effector process. If this process is not present, the
     value is seen as perpetual, with its renewable nature determining the rivality."
	applies to im:Process
	implies ValueExchanged;

abstract role ProvisioningEcosystemBenefit
	"An ecosystem benefit that produces natural value that is directly used by society."
	is Provisioning SocioEcologicalExchange;

abstract role RegulatingEcosystemBenefit
	"An ecosystem benefit that protects independently generated value by mitigating the effects
	 of an effector process."
	is Regulating SocioEcologicalExchange
	implies at least 1 Effector, ValueMaintenance
;