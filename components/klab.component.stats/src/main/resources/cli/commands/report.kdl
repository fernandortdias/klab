/**
 * This file defines the contract for all commands recognized in k.IM. Those with a
 * 'class' specification also have handlers that perform specific tasks when the annotations
 * are encountered.
 */
@klab 0.11.0
@version 0.11.0
@namespace report

void print
	"Print a usage report with the specified options. Use the optional keywords
	 {hourly|daily|monthly|weekly|yearly}, users, {models|resources|observables|operations|observations|downloads|contexts|apps|engines}
	 to define the aggregation type and order. Only one of the asset types 
	 (models|resources|observables|operations|downloads) can be specified in a request."
{

	optional text users
		"Select a specific user or comma-separated list thereof. Use !user to exclude the user" 

	optional text groups
		"Select a specific group or comma-separated list thereof. Use !group to exclude the group" 

	optional text span 
		"Two comma-separated long integers referring to the beginning and end of
	     the reporting period, specified in milliseconds since epoch (UTC). Default is
	     the entire span of the available statistics, only available to
	     administrators. If only one number is given, it is interpreted as the
	     starting time. Alternatively, the span can be given as one of
	     {hour|day|week|month|year}, potentially preceded by an integer multiplier
	     such as in '2,week' to select events in one or more specific spans ending 
	     at the one containing the current date." 

	optional text apps
		"Select a specific app or comma-separated list thereof. Use !app to exclude it"

	optional text 'models'
		"Select a specific model URN or comma-separated list thereof. Use !model to exclude it"

	optional text 'resources'
		"Select a specific resource URN or comma-separated list thereof. Use !urn to exclude it"

	optional text engines
		"Select a specific engine id (use 'local' for a local engine) or comma-separated list thereof.
		 Use !engine to exclude it"

	optional text 'observables'
		"Select a specific observable or comma-separated list thereof. The string will match the concept definition. 
		 Use !concept to exclude it"
	
	optional boolean errors
		"If true, report errors, otherwise report successful observations."
	
	optional boolean html 
		"Output HTML instead of the default Markdown. If the output file is not specified, launch the local browser."

	optional text output
		"File to write the output to (output on console by default for Markdown, in local browser if html)"

	class org.integratedmodelling.stats.commands.PrintReport
}

void on
	"Turn the local reporting feature on. Setting is persisted unless the -t switch is passed. 
	 Must have PostgreSQL running and configured in k.LAB."
{
	optional boolean reset
		"Reset the reference database before starting."

	optional boolean temporary
		"Do not persist the ON setting."

	optional boolean private
		"Do not send reports outside the local instance."

	class org.integratedmodelling.stats.commands.ActivateLocalReporting
}

void off
	"Turn the local reporting feature off, resuming normal operations. Setting is persisted unless the -t switch is passed."
{
	optional boolean temporary
		"Do not persist the OFF setting."

	class org.integratedmodelling.stats.commands.DeactivateLocalReporting
}

void status
	"Print the status of local reporting."
{
	optional boolean temporary
		"Do not persist the OFF setting."

	class org.integratedmodelling.stats.commands.LocalReportingInfo
}