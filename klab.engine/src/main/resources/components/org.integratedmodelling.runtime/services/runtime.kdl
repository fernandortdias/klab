/**
 * This file defines the contract for functions compiled by the runtime into dataflow calls. These
 * are normally not seen by users.
 */
@klab 0.10.0
@version 0.10.0
@namespace klab.runtime

export value exec
	"Return an appropriate contextualizer for an expression."
	label 'Evaluate'
{
	
	value expression code
		"The code to be executed"
		
	optional boolean expression ifcondition 
		"A condition to check before executing the code" 
		default ""

	optional boolean expression unlesscondition 
		"A negative conditions to check before executing the code" 
		default ""

	optional text language 
		"The language of the expression" 
		default "groovy"
		
	class org.integratedmodelling.klab.components.runtime.contextualizers.ExpressionResolver

}

export value literal
	"Return a mediating resolver that wraps a state into another with compatible, but 
	 different value semantics"
	 label '$val'
{
	object val
		"The value to be contextualized"
				
	class org.integratedmodelling.klab.components.runtime.contextualizers.LiteralStateResolver

}

export number convert
	"Return a contextualizer for a literal value"
	label 'Convert $original to $target'
{
	value 'original'
		"The value mediator (unit, range, currency) describing the source values"

	value 'target'
		"The value mediator (unit, range, currency) describing the target values"

	optional import number artifact 
		"The artifact to convert. Defaults to the target artifact."
				
	class org.integratedmodelling.klab.components.runtime.contextualizers.ConversionResolver

}

export concept classify
	"Return a state contextualizer that classifies its input into a concept"
	label 'Classify input'
{
	
	value classification
		"A classification stated in k.IM or built from a trait, class or metadata property"

	class org.integratedmodelling.klab.components.runtime.contextualizers.ClassifyingStateResolver

}

export value lookup
	"Return a state contextualizer that classifies its inputs using a lookup table"
	label 'Look up in table'
{
	
	value table
		"A table stated in k.IM"

	value arguments 
		"A set of arguments to match to the columns of the table"

	class org.integratedmodelling.klab.components.runtime.contextualizers.LookupStateResolver

}

export value resolve
	"Return a resolver for a URN-specified resource"
	label 'Extract data from URN'
{
	
	text urn
		"The URN pointing to the resource to be resolved and retrieved"
				
	class org.integratedmodelling.klab.components.runtime.contextualizers.UrnResolver
}

export object instantiate
	"Return an instantiator for a URN-specified resource"
	label 'Extract objects from URN'
{
	
	text urn
		"The URN pointing to the resource to be resolved and retrieved"
				
	class org.integratedmodelling.klab.components.runtime.contextualizers.UrnInstantiator

}

export value cast
	"Return a state resolver that will convert a value from a type to another"
	label 'Transform $source to $destination'
{
	optional import number artifact 
		"The artifact to cast. Defaults to the target artifact."
	
	// TODO make this an enum
	text source
		"The source type of the value"

	// TODO make this an enum
	text destination
		"The destination type of the value"
				
	class org.integratedmodelling.klab.components.runtime.contextualizers.CastingStateResolver
}

export value conditional
	"Wrap a function call that produces a contextualizer, created only if the passed condition evaluates to true."
{
	
	boolean expression condition
		"The condition for the contextualizer to be returned."
				
	class org.integratedmodelling.klab.components.runtime.contextualizers.wrappers.ConditionalContextualizer
}